apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion commonCompileSdkVersion
    buildToolsVersion commonBuildToolsVersion

    defaultConfig {
        applicationId "com.laithlab.rhythm"

        def localProperties = project.file("onesignal.properties")
        if (localProperties.exists()) {
            Properties properties = new Properties()
            localProperties.withInputStream { instr ->
                properties.load(instr)
            }

            manifestPlaceholders = [manifestApplicationId          : "${applicationId}",
                                    onesignal_app_id               : properties.getProperty('onesignal_app_id'),
                                    onesignal_google_project_number: properties.getProperty('onesignal_google_project_number')]
        }

        minSdkVersion commonMinSdkVersion
        targetSdkVersion commonTargetSdkVersion
        versionCode 5
        versionName "1.0"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "DEV_MODE", "false"
        }

        beta {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "DEV_MODE", "false"
            signingConfig signingConfigs.debug
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "DEV_MODE", "true"
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:${commonSupportLibraryVersion}"
    compile "com.android.support:palette-v7:${commonSupportLibraryVersion}"
    compile "com.android.support:design:${commonSupportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${commonSupportLibraryVersion}"

    compile 'com.onesignal:OneSignal:2.1.1@aar'
    compile "com.google.android.gms:play-services-wearable:${commonPlayServicesVersion}"
    compile "com.google.android.gms:play-services-gcm:${commonPlayServicesVersion}"
    compile "com.google.android.gms:play-services-analytics:${commonPlayServicesVersion}"
    compile "com.google.android.gms:play-services-location:${commonPlayServicesVersion}"

    compile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
    betaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta1'

    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'io.realm:realm-android:0.87.5'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.jakewharton.timber:timber:4.1.1'
    testCompile 'junit:junit:4.12'
}

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.4'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}
